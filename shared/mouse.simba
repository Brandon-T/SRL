(*
  Moving the mouse
  ~~~~~~~~~~~~~~~~

  Move the mouse to a x, y coord.
   - Mouse.Move(100, 100);

  Move the mouse to a point.
   - Mouse.Move(Point(100, 100));

  Move the mouse to a random (gauss) point in a box.
  - Mouse.Move(IntToBox(0, 0, 100, 100));

  Move the mouse to a random (gauss) point in a circle.
  - Mouse.Move(Point(100, 100), 50);

  Clicking the mouse
  ~~~~~~~~~~~~~~~~~~

  Vaild click types: mouse_Left, mouse_Right, mouse_Middle.

  Click the mouse at the current position.
   - Mouse.Click(mouse_Left);

  Move and click the mouse to a x, y coord.
   - Mouse.Click(100, 100, mouse_Left);

  Move and click the mouse to a point.
   - Mouse.Click(Point(100, 100), mouse_Right);

  Move and click the mouse to a random (gauss) point in a box.
  - Mouse.Click(IntToBox(0, 0, 100, 100), mouse_LEft);

  Move and click the mouse to a random (gauss) point in a circle.
  - Mouse.Click(Point(100, 100), 50, mouse_Right);
*)

type
  ERandomStyle = (rndRandom, rndGauss, rndSkewed);
  TMouseMode = (mouse_Human, mouse_Brake, mouse_Accurate);
  TMouseChangeDestEvent = procedure(Sender: TObject; var xe, ye: Extended; var exit_method: Boolean);
  TMouseMoveEvent2 = procedure(Sender: TObject; Shift:TShiftState; X,Y: Int32);
  TMouseEvent2 = procedure(Sender: TObject; Button: TMouseButton; Shift:TShiftState; X,Y:Int32);

  TMouse = record
  {$IFNDEF CODEINSIGHT}
    FSpeed: Integer;
    FMode: TMouseMode;
    FGravity, FWind: array [0..2] of Extended;
    FOnMouseMove: TMouseMoveEvent2;
    FOnMouseEvent: TMouseEvent2;
    FOnMouseChangeDestEvent: TMouseChangeDestEvent;
  {$ENDIF}
  end;

procedure TMouse.__Setup();
begin
  FSpeed := Random(21, 23);
  FMode := mouse_Human;

  FOnMouseMove := nil;
  FOnMouseEvent := nil;
  FOnMouseChangeDestEvenT := nil;

  FGravity[Ord(Mouse_Human)] := 7;
  FGravity[Ord(Mouse_Brake)] := 7;
  FGravity[Ord(Mouse_Accurate)] := 14;

  FWind[Ord(mouse_Human)] := 4;
  FWind[Ord(mouse_Brake)] := 4;
  FWind[Ord(mouse_Accurate)] := 3;
end;

procedure TMouse.ValidateButton(const Button: Integer);
begin
  if (not Button = mouse_Left) or (not Button = mouse_Right) or (not Button = mouse_Middle) then
    RaiseException(erCustomError, 'TMouse.ValidateButton: Mouse button isn''t vaild, use (mouse_Left, mouse_Right, mouse_Middle');
end;

procedure TMouse.SetPosition(const Target: TPoint);
begin
  if (@FOnMouseMove <> nil) then
    FOnMouseMove(@Self, [], Target.X, Target.Y);

  MoveMouse(Target.X, Target.Y);
end;

function TMouse.GetPosition(): TPoint;
begin
  GetMousePos(Result.X, Result.Y);
end;

function TMouse.GetMode(): TMouseMode;
begin
  Result := FMode;
end;

procedure TMouse.SetMode(const NewMode: TMouseMode);
begin
  FMode := NewMode;
end;

function TMouse.GetSpeed(): Integer;
begin
  Result := Self.FSpeed;
end;

procedure TMouse.SetSpeed(const NewSpeed: Integer);
begin
  Self.FSpeed := NewSpeed;
end;

function TMouse.GetGravity(const Mode: TMouseMode): Double;
begin
  Result := Self.FGravity[Ord(Mode)];
end;

procedure TMouse.SetGravity(const Mode: TMouseMode; const NewGravity: Double);
begin
  Self.FGravity[Ord(Mode)] := NewGravity;
end;

function TMouse.GetWind(const Mode: TMouseMode): Double;
begin
  Result := Self.FWind[Ord(Mode)];
end;

procedure TMouse.SetWind(const Mode: TMouseMode; const NewWind: Double);
begin
  Self.FWind[Ord(Mode)] := NewWind;
end;

function TMouse.GetOnMouseMove(): TMouseMoveEvent2;
begin
  Result := @Self.FOnMouseMove;
end;

procedure TMouse.SetOnMouseMove(const Event: TMouseMoveEvent2);
begin
  Self.FOnMouseMove := @Event;
end;

function TMouse.GetOnMouseEvent(): TMouseEvent2;
begin
  Result := @Self.FOnMouseEvent;
end;

procedure TMouse.SetOnMouseEvent(const Event: TMouseEvent2);
begin
  Self.FOnMouseEvent := @Event;
end;

function TMouse.GetOnChangeDestEvent(): TMouseChangeDestEvent;
begin
  Result := @Self.FOnMouseChangeDestEvent;
end;

procedure TMouse.SetOnChangeDestEvent(const Event: TMouseChangeDestEvent);
begin
  Self.FOnMouseChangeDestEvent := @Event;
end;

procedure TMouse.Hold(const Button: Integer);
var
  p: TPoint;
begin
  Self.ValidateButton(Button);

  p := Self.GetPosition();
  HoldMouse(p.x, p.y, Button);
end;

procedure TMouse.Release(const Button: Integer);
var
  p: TPoint;
begin
  Self.ValidateButton(Button);

  p := Self.GetPosition();
  ReleaseMouse(p.x, p.y, Button);
end;

{$IFNDEF CODEINSIGHT}
procedure TMouse._MoveBrake(xs, ys, xe, ye, gravity, wind, targetArea: Extended; doubleUp: Boolean);
const
  sqrt2 := sqrt(2);
  sqrt3 := sqrt(3);
  sqrt5 := sqrt(5);
var
  veloX,veloY,windX,windY,veloMag,baseDist,dist,randomDist,lastDist,D: Extended;
  lastX,lastY,tempSpeed,W,xsR, ysR: Integer;
  PDist,maxStep,dModA,dModB,nModA,nModB: Extended;
  Event, doExit: Boolean;
  t: UInt64;
begin
  tempSpeed := Self.GetSpeed();
  Event := Assigned(@FOnMouseChangeDestEvent);
  baseDist := Max(1,Hypot(xs - xe, ys - ye));

  dModA := 0.88;
  dModB := 0.95;

  if (baseDist > 220) then
  begin
    nModA := 0.08;
    nModB := 0.04;
  end else if (baseDist <= 220) then
  begin
    nModA := 0.20;
    nModB := 0.10;
  end;

  t := GetTickCount64() + Random(9700, 10000);
  repeat
    if (GetTickCount64() > t) then
      RaiseException(erCustomError, GetGlobalName(@Self) + ' timed out');

    if (Event) then
    begin
      FOnMouseChangeDestEvent(@Self, xe, ye, doExit);
      if (doExit) then
        Exit;
    end;

    dist := Max(1,Hypot(xs - xe, ys - ye));
    wind := Min(wind, dist);
    PDist := Max(0.01,(dist/baseDist));

    if doubleUp then
    begin
      if (PDist <= dModA) then
        D := Max(20,Round(dist*0.3)/5)
      else if (PDist > dModA) then
      begin
        if (PDist < dModB) then
          D := RandomRange(5, 8)
        else if (PDist >= dModB) then
          D := RandomRange(3, 4);
      end;
    end;

    if (PDist >= nModA) then
      D := Max(20,round(dist*0.3)/5)
    else if (PDist < nModA) then
    begin
      if (PDist >= nModB) then
        D := RandomRange(5, 8)
      else if (PDist < nModB) then
        D := RandomRange(3, 4);
    end;

    if (D <= Round(dist)) then
      maxStep := D
    else
      maxStep := Round(dist);

    if dist >= targetArea then
    begin
      windX := windX / sqrt3 + (random(round(wind) * 2 + 1) - wind) / sqrt5;
      windY := windY / sqrt3 + (random(round(wind) * 2 + 1) - wind) / sqrt5;
    end else
    begin
      windX := windX / sqrt2;
      windY := windY / sqrt2;
    end;

    veloX += windX;
    veloY += windY;
    veloX += gravity * (xe - xs) / dist;
    veloY += gravity * (ye - ys) / dist;

    if (Hypot(veloX, veloY) > maxStep) then
    begin
      randomDist := maxStep / 2.0 + Random(Round(maxStep) div 2);
      veloMag := Sqrt(veloX * veloX + veloY * veloY);
      veloX := (veloX / veloMag) * randomDist;
      veloY := (veloY / veloMag) * randomDist;
    end;

    lastX := Round(xs);
    lastY := Round(ys);
    xs := xs + veloX;
    ys := ys + veloY;
    xsR := Round(xs);
    ysR := Round(ys);

    if (lastX <> xsR) or (lastY <> ysR) then
      Self.SetPosition([xsR, ysR]);

    W := Max(4,Random(Round(100/tempSpeed))*6);
    if (doubleUp) then        //uhhuh..
      if (PDist > dModA) then
        W := Round(W*2.5)
    else
      W := Round(W*1.2);
    Wait(W);

    lastdist := dist;
  until Hypot(xs - xe, ys - ye) < 1;

  if (Round(xe) <> Round(xs)) or (Round(ye) <> Round(ys)) then
    Self.SetPosition([Round(xe), Round(ye)]);

  Self.SetSpeed(tempSpeed);
end;
{$ENDIF}

{$IFNDEF CODEINSIGHT}
procedure TMouse._MoveHuman(xs, ys, xe, ye, gravity, wind, targetArea:Extended);
const
  sqrt2 := sqrt(2);
  sqrt3 := sqrt(3);
  sqrt5 := sqrt(5);
var
  veloX,veloY,windX,windY,veloMag,baseDist,dist,randomDist,lastDist,D,maxStep: Extended;
  lastX,lastY, tempSpeed,W, xsR, ysR: Int32;
  Event, doExit: Boolean;
  t: UInt64;
begin
  tempSpeed := Self.getSpeed();
  Event := Assigned(@FOnMouseChangeDestEvent);

  baseDist := Hypot(xs - xe, ys - ye);
  t := GetTickCount64() + Random(9700, 10000);
  repeat
    if (GetTickCount64() > t) then
      RaiseException(erCustomError, GetGlobalName(@Self) + ' timed out');

    if (Event) then
    begin
      FOnMouseChangeDestEvent(@Self, xe, ye, doExit);
      if (doExit) then
        Exit;
    end;

    dist := Hypot(xs - xe, ys - ye);
    wind := Min(wind, dist);
    if (dist < 1) then
      dist := 1;

    if Random(6) = 1 then
      D := RandomRange(2,3)
    else
      D :=  Max(5,Min(Round(baseDist*0.3)/7,25));

    if (D <= Round(dist)) then
      maxStep := D
    else
      maxStep := round(dist);

    if dist >= targetArea then
    begin
      windX := windX / sqrt3 + (Random(Round(wind) * 2 + 1) - wind) / sqrt5;
      windY := windY / sqrt3 + (Random(Round(wind) * 2 + 1) - wind) / sqrt5;
    end else
    begin
      windX := windX / sqrt2;
      windY := windY / sqrt2;
    end;

    veloX += windX;
    veloY += windY;
    veloX += gravity * (xe - xs) / dist;
    veloY += gravity * (ye - ys) / dist;

    if (Hypot(veloX, veloY) > maxStep) then
    begin
      randomDist := maxStep / 2.0 + Random(Round(maxStep) div 2);
      veloMag := Sqrt(veloX * veloX + veloY * veloY);
      veloX := (veloX / veloMag) * randomDist;
      veloY := (veloY / veloMag) * randomDist;
    end;

    lastX := Round(xs);
    lastY := Round(ys);
    xs += veloX;
    ys += veloY;
    xsR := Round(xs);
    ysR := Round(ys);

    if (lastX <> xsR) or (lastY <> ysR) then
      Self.SetPosition([xsR, ysR]);

    W := Round(Max(4, Random(Round(100/tempSpeed))*6) );
    Wait(Round(W * 0.7));

    lastdist := dist;
  until Hypot(xs - xe, ys - ye) < 1;

  if (Round(xe) <> Round(xs)) or (Round(ye) <> Round(ys)) then
    Self.SetPosition([Round(xe), Round(ye)]);

  Self.SetSpeed(tempSpeed);
end;
{$ENDIF}

procedure TMouse.Move(const Target: TPoint);
var
  RandSpeed: Extended;
  p: TPoint;
begin
  RandSpeed := (Random(Self.getSpeed()) / 2.0 + Self.getSpeed()) / 10.0;
  p := Self.getPosition();

  case (Self.getMode()) of
    mouse_Human:
      Self._MoveHuman(p.X, p.Y, Target.X, Target.Y, FGravity[Ord(mouse_Human)], FWind[Ord(mouse_Human)], 10.0 * randSpeed);
    mouse_Brake:
      Self._MoveBrake(p.X, p.Y, Target.X, Target.Y, FGravity[Ord(mouse_Brake)], FWind[Ord(mouse_Brake)], 10.0 * randSpeed, False);
    mouse_Accurate:
      Self._MoveBrake(p.X, p.Y, Target.X, Target.Y, FGravity[Ord(mouse_Accurate)], FWind[Ord(mouse_Accurate)], 10.0 * randSpeed, False);
  end;
end;

procedure TMouse.Move(const X, Y: Integer); overload;
begin
  Self.Move(Point(X, Y));
end;

procedure TMouse.Move(const Box: TBox; ForcedMove:Boolean=False; RandomStyle:ERandomStyle = rndGauss); overload;
begin
  if PointInBox(Self.GetPosition(),Box) and (not ForcedMove) then
    Exit();
  
  case RandomStyle of
    rndRandom: Self.Move(Point(Random(Box.X1, Box.X2), Random(Box.Y1, Box.Y2)));
    rndGauss:  Self.Move(srl.RandomPoint(Box));
    rndSkewed: Self.Move(srl.RandomPointEx(Self.GetPosition(), Box));
  end;
end;

procedure TMouse.Move(const Middle: TPoint; const Radius: Int32; ForcedMove:Boolean=False); overload;
var PT: TPoint;
begin
  if not ForcedMove then
  begin
    PT := self.GetPosition();
    if Sqr(PT.X - Middle.X) + Sqr(PT.Y - Middle.Y) <= Sqr(Radius) then
      Exit;
  end;
  Self.Move(srl.RandomPoint(Middle, Radius));
end;

procedure TMouse.Click(const Button: Integer);
var
  p: TPoint;
begin
  Self.ValidateButton(Button);
  p := Self.getPosition();

  if (Assigned(@FOnMouseEvent)) then
    Self.FOnMouseEvent(@Self, TMouseButton(Abs(Abs(not Button) - 2)), [], p.X, p.Y);

  Self.Hold(Button);
  Wait(Trunc(srl.SkewedRand(50,15,370)));
  Self.Release(Button);
end;

procedure TMouse.Click(const Target: TPoint; const Button: Integer); overload;
var
  p: TPoint;
begin
  Self.ValidateButton(Button);
  p := Self.getPosition();

  if (p.X <> Target.X) or (p.Y <> Target.Y) then
  begin
    Self.Move(Target.X, Target.Y);
    Wait(Random(15,80));
  end;

  Self.Click(Button);
end;

procedure TMouse.Click(const X, Y: Integer; const Button: Integer); overload;
begin
  Self.ValidateButton(Button);
  Self.Click(Point(X, Y), Button);
end;

procedure TMouse.Click(const Box: TBox; const Button: Integer; ForcedMove:Boolean=False; RandomStyle:ERandomStyle = rndGauss); overload;
begin
  Self.Move(Box, ForcedMove, RandomStyle);
  Self.Click(Button);
end;

procedure TMouse.Click(const Middle: TPoint; const Radius, Button: Integer); overload;
begin
  Self.Click(srl.RandomPoint(Middle, Radius), Button);
end;

procedure TMouse.Miss(const Target: TPoint; const Correct: Boolean = False; const WaitAfterMiss: Integer = 0);
var
  randSpeed: Extended;
  dist,mp, a: integer;
  p: TPoint;
begin
  a := Self.getSpeed();
  dist := Distance(p.X, p.Y, Target.X, Target.Y);
  mp := round(dist / 150);
  if (mp < 0) then
    mp := 1;

  randSpeed := (Random(a) / 2.0 + a) / 10.0;
  Self.Move(Random(Target.x-(a*mp), Target.x+(a*mp)), Random(Target.y-(a*mp), Target.y+(a*mp)));

  if (Correct) then
  begin
    Wait(WaitAfterMiss);
    Self.Move(Target);
  end;
end;

procedure TMouse.DragTo(const Target: TPoint; const Button: Integer = mouse_Left);
begin
  Self.ValidateButton(Button);

  Self.Hold(Button);
  Self.Move(Target);
  Self.Release(Button);
end;

procedure TMouse.Scroll(Scrolls: Integer; Down: Boolean);
var
  i,step,k:Int32=1;
  p:TPoint;
  downInt, upInt: Integer;
begin
  {$IFDEF LINUX}
    downInt := 1; upInt := -1;
  {$ELSE}
  {$IFDEF SMART}
    downInt := 1; upInt := -1;
  {$ELSE}
    downInt := -1; upInt := 1;
  {$ENDIF}
  {$ENDIF}

  p := Self.GetPosition();
  step := Round(srl.GaussRand(6.0,0.6));
  for i:=1 to Scrolls do
  begin
    if Down then
      ScrollMouse(p.x, p.y, downInt)
    else
      ScrollMouse(p.x, p.y, upInt);

    if k mod step = 0 then
    begin
      step := Round(srl.GaussRand(6.0,0.6));
      srl.NormalWait(215,410);
      k := 0;
    end else
      srl.NormalWait(25,46);
    Inc(k);
  end;
end;

procedure TMouse.Scroll(Target: TPoint; scrolls: Integer; Down: Boolean); overload;
begin
  Self.Move(Target);
  Self.Scroll(Scrolls, Down);
end;

procedure TMouse.Scroll(Box: TBox; scrolls: Integer; Down: Boolean); overload;
begin
  Self.Move(Box);
  Self.Scroll(Scrolls, Down);
end;


var
  Mouse: TMouse;

begin
  Mouse.__Setup();
end;
