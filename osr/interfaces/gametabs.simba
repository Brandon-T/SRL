type
  EGametab = (
    tabCombat,
    tabStats,
    tabQuests,
    tabInventory,
    tabEquipment,
    tabPrayer,
    tabMagic,
    tabClan,
    tabFriends,
    tabIgnores,
    tabLogout,
    tabOptions,
    tabEmotes,
    tabMusic
  );

  TRSGametab = record
    Tabs: TBoxArray;
    Keybindings: TIntegerArray;
    FKeysEnabled: Boolean;
  end;


  TGtButton = record
    stateEnabled: UInt32;
    stateDisabled: UInt32;
    bounds: TBox;
  end;

var
  Gametabs: TRSGametab;

procedure TRSGametab.__setup();
var
  Upper,Lower:TBox;
begin
  with Self do
  begin
    Upper := [527,168,760,204];
    Lower := [527,466,760,502];

    Tabs := Upper.Partition(1, 7, -3);
    Tabs.Extend(Lower.Partition(1, 7, -3));

    Keybindings := [VK_F1, VK_F2, VK_F3, VK_ESCAPE, VK_F4, VK_F5, VK_F6, VK_F7,
                    VK_F8, VK_F9, 0, VK_F10, VK_F11, VK_F12];
  end;
end;

procedure TRSGametab.ToggleFKeys(const Enable:Boolean);
begin
  Self.FKeysEnabled := Enable;
end;

function TRSGametab.GetBounds(Tab:EGametab): TBox;
begin
  Result := Self.Tabs[Ord(Tab)];
end;

function TRSGametab.IsOpen(Tab:EGametab; maxWait:Int32=0): Boolean;
var
  FoundTab:Boolean;
  i,t:Int64;
begin
  t := GetTickCount64() + maxWait;
  repeat
    if CountColorTolerance(1910383, self.GetBounds(Tab), 13) > 0 then
      Exit(True)
    else if (Tab = tabInventory) then //when in bank no tabs exist
    begin
      FoundTab := False;
      for i:=0 to High(Self.Tabs) do
        if (CountColorTolerance(1910383, Self.Tabs[i], 13) > 0) then
        begin
          FoundTab := True;
          break;
        end;
      if not FoundTab then
        Exit(True);
    end;
    Wait(Random(50,90));
  until GetTickCount64() > t;
end;

function TRSGametab.Open(Tab:EGametab; tries:Int32=2): Boolean;
var
  i:Int32;
  B:TBox;
begin
  B := self.getBounds(Tab);
  if Self.IsOpen(Tab) then
    Exit(True);

  for i:=1 to tries do
  begin
    if (Self.FKeysEnabled and (Keybindings[Ord(tab)] <> 0)) then
    begin
      Keyboard.PressKey(Keybindings[Ord(tab)]);
      if self.IsOpen(Tab, Random(1500,2500)) then
        Exit(True);
      Self.ToggleFKeys(False);
    end;

    Mouse.Click(B, 1);
    if self.IsOpen(Tab, Random(1500,2500)) then
      Exit(True);
  end;
end;


procedure TRSGametab.Hover(Tab:EGametab; Condition:Boolean=True);
begin
  if Condition then
    Mouse.Move(self.GetBounds(Tab));
end;


function TRSGametab.GetCurrent(): EGametab;
var i:Int32;
begin
  for i:=0 to Ord(High(EGameTab)) do
    if Self.IsOpen(EGameTab(i)) then
      Exit(EGameTab(i));
end;


//----------------

procedure TGtButton.Init(B:TBox; enaColor:Int32=1777019; disColor:Int32=5067346);
begin
  self.stateEnabled := enaColor;
  self.stateDisabled := disColor;
  self.bounds := B;
end;


function GtButton(B:TBox; enaColor:Int32=1777019; disColor:Int32=5067346): TGtButton;
begin
  Result.Init(B,enaColor,disColor);
end;


//Checks if the given button (by bounds) is red/enabled
function TGtButton.IsEnabled(): Boolean; constref;
begin
  Result := CountColorTolerance(self.stateEnabled, self.bounds, 30) > 70;
end;

//helper: Clicks the given button and returns True if it didn't fail
function TGtButton.Toggle(out enabled:Boolean; tryTime:Int32=2000): Boolean; constref;
var t:UInt64;
begin
  enabled := not(Self.IsEnabled());
  mouse.click(self.bounds, mouse_left);
  t := GetTickCount64()+tryTime;
  repeat
    Result := Self.IsEnabled() = enabled;
    if not(Result) then Wait(25);
  until (GetTickCount64() > t) or Result;
end;




begin
  Gametabs.__setup();
end;

{$include_once gametabs/combat.simba}
{$include_once gametabs/stats.simba}
{$include_once gametabs/inventory.simba}
{$include_once gametabs/equipment.simba}
{$include_once gametabs/prayer.simba}
{$include_once gametabs/spells.simba}

{$include_once gametabs/logout.simba}
{$include_once gametabs/options.simba}
