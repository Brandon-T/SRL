(*
Prayer
=======
The file holds functions and procedures that are related to the prayer gametab
*)

type 
  TRSPrayer = record(TInterfaceBase)
    FSlots: TBoxArray;
  end;

  
{==| Table of Contents |=======================
 procedure TRSPrayer.__setup();
 function TRSPrayer.Open(): Boolean;
 function TRSPrayer.IsOpen(): Boolean;
 function TRSPrayer.GetPrayerBound(prayer: EPrayerType): TBox;
 function TRSPrayer.CanActivate(prayer: EPrayerType): Boolean;
 function TRSPrayer.MouseOver(prayer: EPrayerType): Int32;
 function TRSPrayer.Toggle(prayer: EPrayerType): Boolean;
}

(*
var Prayer
~~~~~~~~~~~
Variable that stores functions and properties of the Runescape prayer gametab.
*)
var 
  Prayer: TRSPrayer;

type
  EPrayerType = (
    PRAY_THICK_SKIN,
    PRAY_BURST_OF_STRENGTH,
    PRAY_CLARITY_OFTHOUGHT,
    PRAY_SHARP_EYE,
    PRAY_MYSTIC_WILL,
    
    PRAY_ROCK_SKIN,
    PRAY_SUPERHUMAN_STRENGTH,
    PRAY_IMPROVED_REFLEXES,
    PRAY_RAPID_RESTORE,
    PRAY_RAPID_HEAL,
    
    PRAY_PROTECT_ITEM,
    PRAY_HAWK_EYE,
    PRAY_MYSTIC_LORE,
    PRAY_STEEL_SKIN,
    PRAY_ULTIMATE_STRENGTH,
    
    PRAY_INCREDIBLE_REFLEXES,
    PRAY_PROTECT_FROM_MAGIC,
    PRAY_PROTECT_FROM_MISSILES,
    PRAY_PROTECT_FROM_MELEE,
    PRAY_EAGLE_EYE,
    
    PRAY_MYSTIC_MIGHT,
    PRAY_RETRIBUTION,
    PRAY_REDEMPTION,
    PRAY_SMITE,
    PRAY_CHIVALRY,
    
    PRAY_PIETY
  );


{$IFNDEF CODEINSIGHT}
procedure TRSPrayer.__Setup();
var
  SlotBounds: TBox = [550,213,734,433];
begin
  with Self do
  begin
    FName := 'Prayer';
    FStatic := True;
    FBounds := [548,206,735,464];
    FSlots := SlotBounds.Partition(6,5,-3);
  end;
end;

procedure TRSPrayer.Debug();
var
  BMP:TMufasaBitmap;
  i:Int32;
begin
  BMP := GetMufasaBitmap( BitmapFromClient(SRL.DefaultClientBounds) );
  BMP.DrawBoxes(Self.FSlots, False, $0000FF);
  BMP.DrawBox(Self.FBounds, False, $00FF00);
  if Self.IsOpen() then
    for i:=0 to High(Self.FSlots) do
      if self.CanActivate(EPrayerType(i)) then
        BMP.DrawBox(Self.FSlots[i], False, $FFFFFF);

  BMP.Debug();
  BMP.Free();
end;
{$ENDIF}

(*
Prayer.Open
~~~~~~~~~~~~~~~~~~~~~~~~
.. code-block:: pascal

  function TRSPrayer.Open(): Boolean;

Opens the interface
*)
function TRSPrayer.Open(): Boolean;
begin
  Result := Gametabs.Open(tabPrayer);
end;


(*
Prayer.IsOpen
~~~~~~~~~~~~~~~~~~~~~~~~
.. code-block:: pascal

  function TRSPrayer.IsOpen(maxWait:Int32=0): Boolean;

Checks if the interface is already open.
*)
function TRSPrayer.IsOpen(maxWait:Int32=0): Boolean;
begin
  Result := Gametabs.IsOpen(tabPrayer, maxWait);
end;


(*
Prayer.GetPrayerBound
~~~~~~~~~~~~~~~~~~~~~~~~
.. code-block:: pascal

  function TRSPrayer.GetPrayerBound(prayer: EPrayerType): TBox;

Returns the bounding box covering the given prayer.

.. note:: by slacky
*)
function TRSPrayer.GetPrayerBound(prayer: EPrayerType): TBox;
begin
  Result := Self.FSlots[prayer];
end;

(*
Prayer.CanActivate
~~~~~~~~~~~~~~~~~~~
.. code-block:: pascal

  function TRSPrayer.CanActivate(prayer: EPrayerType): Boolean;

Returns True if you can activete the prayer..

.. note:: by slacky
*)
function TRSPrayer.CanActivate(prayer: EPrayerType): Boolean;
var 
  bmp: TMufasaBitmap;
begin
  if not(self.Open()) then Exit(False);
  bmp.FromClient(self.GetPrayerBound(prayer));
  Result := bmp.PeakBrightness() > 35;
  bmp.Free();
end;

(*
Prayer.IsActivated
~~~~~~~~~~~~~~~~~~~
.. code-block:: pascal

  function TRSPrayer.IsActivated(prayer: EPrayerType): Boolean;

Returns True if the spell is activated

.. note:: by slacky
*)
function TRSPrayer.IsActivated(prayer: EPrayerType): Boolean;
begin
  if not(self.Open()) then Exit(False);
  Result := CountColorTolerance(7185335, self.GetPrayerBound(prayer), 10) > 50;
end;


(*
Prayer.MouseOver
~~~~~~~~~~~~~~~~~
.. code-block:: pascal

  function TRSPrayer.MouseOver(prayer: EPrayerType): Boolean;

Moves the mouse over the given prayer.

.. note:: by slacky
*)
function TRSPrayer.MouseOver(prayer: EPrayerType): Boolean;
begin
  if not(self.Open()) then Exit();
  
  Result := True;
  Mouse.Move(Self.FSlots[prayer]);
end;


(*
Prayer.Toggle
~~~~~~~~~~~~~~
.. code-block:: pascal

  function TRSPrayer.Toggle(prayer: EPrayerType): Boolean;

Click's it if it can be activated.

.. note:: by slacky
*)
function TRSPrayer.Toggle(prayer: EPrayerType): Boolean;
begin
  Result := self.CanActivate(prayer);
  if Result then
    Mouse.Click(Self.FSlots[prayer], mouse_left, False, rndSkewed);
end;


begin
  Prayer.__Setup();
end;
